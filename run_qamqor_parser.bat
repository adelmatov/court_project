@echo off
REM ========== УСТАНОВКА UTF-8 ДЛЯ WINDOWS ==========
chcp 65001 > nul
set PYTHONIOENCODING=utf-8

cd /d "C:\Users\adelmatov001\court_project"

REM Создаем папку logs если её нет
if not exist "logs" mkdir "logs"

REM Активация виртуального окружения
call venv\Scripts\activate.bat

REM ========== ОЧИСТКА СТАРЫХ ЛОГОВ (Python) ==========
python -u -m utils.cleanup_logs logs 3

REM ========== ИМЯ ФАЙЛА ЛОГА ==========
set LOG_FILE=logs\orchestrator_%date:~-4,4%%date:~-7,2%%date:~-10,2%_%time:~0,2%%time:~3,2%%time:~6,2%.log
set LOG_FILE=%LOG_FILE: =0%

echo ================================================ >> "%LOG_FILE%"
echo ОРКЕСТРАТОР - ЗАПУСК ВСЕХ ПАРСЕРОВ >> "%LOG_FILE%"
echo Время: %date% %time% >> "%LOG_FILE%"
echo ================================================ >> "%LOG_FILE%"
echo.

echo ================================================
echo ОРКЕСТРАТОР - ЗАПУСК ВСЕХ ПАРСЕРОВ
echo Время: %date% %time%
echo Лог: %LOG_FILE%
echo ================================================
echo.

REM =============================================================================
REM ШАГ 1: Запуск первого парсера (qamqor_parser) - поиск проверок
REM =============================================================================
echo [1/4] Запуск первого парсера (поиск проверок)...
echo [1/4] Запуск первого парсера (поиск проверок)... >> "%LOG_FILE%"
echo --------------------------------------------- >> "%LOG_FILE%"

python -u -m parsers.qamqor.qamqor_parser >> "%LOG_FILE%" 2>&1

if %ERRORLEVEL% EQU 0 (
    echo [OK] Первый парсер завершен успешно
    echo [OK] Первый парсер завершен успешно >> "%LOG_FILE%"
) else (
    echo [ОШИБКА] Первый парсер завершен с ошибкой: %ERRORLEVEL%
    echo [ОШИБКА] Первый парсер завершен с ошибкой: %ERRORLEVEL% >> "%LOG_FILE%"
)
echo.

REM =============================================================================
REM ШАГ 2: Запуск второго парсера (updater) - обновление старых записей
REM =============================================================================
echo [2/4] Запуск парсера 2 (updater)...
echo [2/4] Запуск парсера 2 (updater)... >> "%LOG_FILE%"
echo --------------------------------------------- >> "%LOG_FILE%"

python -u -m parsers.qamqor.qamqor_updater --status "1" >> "%LOG_FILE%" 2>&1

if %ERRORLEVEL% EQU 0 (
    echo [OK] Парсер 2 завершен успешно
    echo [OK] Парсер 2 завершен успешно >> "%LOG_FILE%"
) else (
    echo [ОШИБКА] Парсер 2 завершен с ошибкой: %ERRORLEVEL%
    echo [ОШИБКА] Парсер 2 завершен с ошибкой: %ERRORLEVEL% >> "%LOG_FILE%"
)
echo.

REM =============================================================================
REM ШАГ 3: Запуск третьего парсера (company_info) - получение данных о компаниях
REM =============================================================================
echo [3/4] Запуск парсера 3 (company_info)...
echo [3/4] Запуск парсера 3 (company_info)... >> "%LOG_FILE%"
echo --------------------------------------------- >> "%LOG_FILE%"

python -u -m parsers.company_info.main --source qamqor >> "%LOG_FILE%" 2>&1

if %ERRORLEVEL% EQU 0 (
    echo [OK] Парсер 3 завершен успешно
    echo [OK] Парсер 3 завершен успешно >> "%LOG_FILE%"
) else (
    echo [ОШИБКА] Парсер 3 завершен с ошибкой: %ERRORLEVEL%
    echo [ОШИБКА] Парсер 3 завершен с ошибкой: %ERRORLEVEL% >> "%LOG_FILE%"
)
echo.

REM =============================================================================
REM ШАГ 4: Запуск скрипта-сборщика данных в Excel
REM =============================================================================
echo [4/4] Запуск сборщика данных в Excel...
echo [4/4] Запуск сборщика данных в Excel... >> "%LOG_FILE%"
echo --------------------------------------------- >> "%LOG_FILE%"

python -u -m parsers.qamqor.qamqor_data_to_excel_collector >> "%LOG_FILE%" 2>&1

if %ERRORLEVEL% EQU 0 (
    echo [OK] Сборщик завершен успешно
    echo [OK] Сборщик завершен успешно >> "%LOG_FILE%"
) else (
    echo [ОШИБКА] Сборщик завершен с ошибкой: %ERRORLEVEL%
    echo [ОШИБКА] Сборщик завершен с ошибкой: %ERRORLEVEL% >> "%LOG_FILE%"
)
echo.

REM =============================================================================
REM ЗАВЕРШЕНИЕ
REM =============================================================================
echo ================================================ >> "%LOG_FILE%"
echo ОРКЕСТРАТОР ЗАВЕРШЕН >> "%LOG_FILE%"
echo Время: %date% %time% >> "%LOG_FILE%"
echo ================================================ >> "%LOG_FILE%"

echo ================================================
echo ОРКЕСТРАТОР ЗАВЕРШЕН
echo Время: %date% %time%
echo ================================================
echo.
echo Лог сохранён: %LOG_FILE%
echo.

call venv\Scripts\deactivate.bat

REM pause